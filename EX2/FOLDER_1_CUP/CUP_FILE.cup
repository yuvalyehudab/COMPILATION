/*************/
/* IMPORT(S) */
/*************/
import java_cup.runtime.*;
import java.io.PrintWriter;
import AST.*;

parser code 
{:
	public Lexer lexer;
	private PrintWriter writer;

	public Parser(Lexer lexer, PrintWriter writer)
	{
		super(lexer);
		this.lexer = lexer;
		this.writer = writer;
	}
	public void report_error(String message, Object info)
	{
		System.out.print("ERROR >> ");		
		System.out.print("[");
		System.out.print(lexer.getLine());
		System.out.print(":");
		System.out.print(lexer.getCharPos());
		System.out.print("] ");
		
		writer.print("ERROR(");
		writer.print(lexer.getLine());
		writer.print(")");
		writer.close();
		
		System.exit(0);
	}
:}

/************************/
/* CUSTOMIZE CUP PARSER */
/************************/
scan with
{:
	Symbol s;
	s = lexer.next_token();
	//System.out.print(s.sym);
	System.out.print("[");
	System.out.print(lexer.getLine());
	System.out.print(":");
	System.out.print(lexer.getCharPos());
	System.out.print("] ");
	System.out.print(TokenNames.terminalNames[s.sym]);
	if (s.value != null)
	{
		System.out.print("( ");
		System.out.print(s.value);
		System.out.print(" )");
	}
	System.out.print("\n");
	return s; 
:};

/*************/
/* TERMINALS */
/*************/
terminal IF;
terminal EQ;
terminal DOT;
terminal PLUS;
terminal MINUS;
terminal TIMES;
terminal WHILE;
terminal DIVIDE;
terminal LPAREN;
terminal RPAREN;
terminal LBRACK;
terminal RBRACK;
terminal LBRACE;
terminal RBRACE;
terminal SEMICOLON;
terminal ASSIGN;
terminal CLASS;
terminal NIL;
terminal ARRAY;
terminal EXTENDS;
terminal RETURN;
terminal NEW;
terminal COMMA;
terminal ELLIPSIS;
terminal LT;
terminal GT;

/*************/
/* TERMINALS */
/*************/
terminal Integer INT;
terminal String ID;
terminal String STRING;

/*****************/
/* NON TERMINALS */
/*****************/
non terminal AST_EXP exp;
non terminal AST_VAR var;

non terminal AST_STMT stmt;
non terminal AST_STMT_LIST stmtList;

non terminal AST_EXP_NEW newExp;

non terminal AST_DEC cField;
non terminal AST_DEC dec;

non terminal AST_CFIELD_LIST cFieldList;
non terminal AST_EXP_LIST expList;
non terminal AST_ID_LIST idList;
non terminal AST_DEC_LIST decList;

non terminal AST_VAR_DEC varDec;
non terminal AST_FUNC_DEC funcDec;
non terminal AST_CLASS_DEC classDec;
non terminal AST_ARRAY_DEC arrayDec;

/***********************/
/* OPERATOR PRECEDENCE */
/***********************/
precedence left ASSIGN;
precedence left EQ;
precedence left LT, GT;
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE;
precedence left LBRACK;
precedence left LPAREN;
precedence left DOT;

/************************/
/* START WITH: decList  */
/************************/
start with decList;

/********************/
/* DERIVATION RULES */
/********************/

decList     ::=         dec:d decList:l     {: RESULT = new AST_DEC_LIST(d,l);    :}
                    |   dec:d               {: RESULT = new AST_DEC_LIST(d,null); :}
                    ;

dec         ::=         varDec:d        {: RESULT = new AST_DEC_VAR(d);    :}
                    |   funcDec:d       {: RESULT = new AST_DEC_FUNC(d);   :}
                    |   classDec:d      {: RESULT = new AST_DEC_CLASS(d);  :}
                    |   arrayDec:d      {: RESULT = new AST_DEC_ARRAY(d);  :}
                    ;

varDec      ::=         ID:type ID:name                  SEMICOLON      {: RESULT = new AST_VAR_DEC_SIMPLE(type,name);    :}
                    |   ID:type ID:name ASSIGN exp:e     SEMICOLON      {: RESULT = new AST_VAR_DEC_EXP(type,name,e);      :}
                    |   ID:type ID:name ASSIGN newExp:ne SEMICOLON      {: RESULT = new AST_VAR_DEC_NEWEXP(type,name,ne);  :}
                    ;

funcDec     ::=         ID:type ID:name LPAREN            RPAREN LBRACE stmtList:stmts RBRACE   {: RESULT = new AST_FUNC_DEC(type, name, null,stmts);:}
                    |   ID:type ID:name LPAREN idList:ids RPAREN LBRACE stmtList:stmts RBRACE   {: RESULT = new AST_FUNC_DEC(type, name, ids,stmts); :}
                    ;

classDec    ::=         CLASS ID:name                   LBRACE cFieldList:l RBRACE      {: RESULT = new AST_CLASS_DEC(name,null,l);     :}
                    |   CLASS ID:name EXTENDS ID:parent LBRACE cFieldList:l RBRACE      {: RESULT = new AST_CLASS_DEC(name,parent,l);   :}
                    ;

arrayDec    ::=         ARRAY ID:name EQ ID:thing LBRACK RBRACK     {: RESULT = new AST_ARRAY_DEC(name,thing);:}
                    ;

exp			::=		    var:v					                {: RESULT = new AST_EXP_VAR(v);          :}
					|   LPAREN exp:e RPAREN                     {: RESULT = new AST_EXP_PAREN(e);        :}
					|   exp:e1 PLUS   exp:e2		    		{: RESULT = new AST_EXP_BINOP(e1, e2, 0);:}
					|   exp:e1 MINUS  exp:e2		    		{: RESULT = new AST_EXP_BINOP(e1, e2, 1);:}
					|   exp:e1 TIMES  exp:e2			    	{: RESULT = new AST_EXP_BINOP(e1, e2, 2);:}
					|   exp:e1 DIVIDE exp:e2				    {: RESULT = new AST_EXP_BINOP(e1, e2, 3);:}
					|   exp:e1 EQ     exp:e2	    			{: RESULT = new AST_EXP_BINOP(e1, e2, 4);:}
					|   exp:e1 LT     exp:e2		    		{: RESULT = new AST_EXP_BINOP(e1, e2, 5);:}
					|   exp:e1 GT     exp:e2			    	{: RESULT = new AST_EXP_BINOP(e1, e2, 6);:}
					|   ID:name LPAREN              RPAREN   	{: RESULT = new AST_EXP_FUNC(name, null);:}
					|   ID:name LPAREN expList:args RPAREN   	{: RESULT = new AST_EXP_FUNC(name, args);:}
					|   var:v DOT ID:name LPAREN              RPAREN {: RESULT = new AST_EXP_FUNC(v,name, null);:}
					|   var:v DOT ID:name LPAREN expList:args RPAREN {: RESULT = new AST_EXP_FUNC(v,name, args);:}
                    |         INT:i							    {: RESULT = new AST_EXP_INT(i,false);    :}
                    |   MINUS INT:i                             {: RESULT = new AST_EXP_INT(i,true);     :}
                    |   NIL                                     {: RESULT = new AST_EXP_NIL();           :}
                    |   STRING:s                                {: RESULT = new AST_EXP_STRING(s);       :}
					;

var			::=		    ID:name					    {: RESULT = new AST_VAR_SIMPLE(name);       :}
					|   var:v DOT ID:fieldName		{: RESULT = new AST_VAR_FIELD(v,fieldName); :}
					|   var:v LBRACK exp:e RBRACK	{: RESULT = new AST_VAR_SUBSCRIPT(v,e);     :}
					;

stmt		::=		    varDec:d                                                    {: RESULT = new AST_STMT_VAR_DEC(d);            :}
                    |   var:v ASSIGN exp:e     SEMICOLON							{: RESULT = new AST_STMT_ASSIGN(v,e);           :}
                    |   var:v ASSIGN newExp:ne SEMICOLON                            {: RESULT = new AST_STMT_ASSIGN_NEW_EXP(v,ne);  :}
					|   RETURN       SEMICOLON                                      {: RESULT = new AST_STMT_RETURN(null);          :}
					|   RETURN exp:e SEMICOLON                                      {: RESULT = new AST_STMT_RETURN(e);             :}
					|   IF    LPAREN exp:cond RPAREN LBRACE stmtList:body RBRACE	{: RESULT = new AST_STMT_IF(   cond,body);      :}
					|   WHILE LPAREN exp:cond RPAREN LBRACE stmtList:body RBRACE	{: RESULT = new AST_STMT_WHILE(cond,body);      :}
					|   			ID:name LPAREN 				RPAREN SEMICOLON {: RESULT = new AST_STMT_FUNC(name,null);  :}
					|				ID:name LPAREN expList:args RPAREN SEMICOLON {: RESULT = new AST_STMT_FUNC(name,args);  :}
					|	var:v DOT	ID:name LPAREN				RPAREN SEMICOLON {: RESULT = new AST_STMT_FUNC(v,name,null);:}
					|   var:v DOT 	ID:name LPAREN expList:args RPAREN SEMICOLON {: RESULT = new AST_STMT_FUNC(v,name,args);:}
					;

newExp      ::=         NEW ID:name                     {: RESULT = new AST_EXP_NEW_SIMPLE(name);:}
                    |   NEW ID:name LBRACK exp:e RBRACK {: RESULT = new AST_EXP_NEW_EXP(name, e);:}
                    ;

cField      ::=         varDec:d    {: RESULT = new AST_DEC_VAR(d);  :}
                    |   funcDec:d   {: RESULT = new AST_DEC_FUNC(d); :}
                    ;

/** Helpers **/

idList      ::=         ID:type ID:name COMMA idList:l  {: RESULT = new AST_ID_LIST(type,name,l);   :}
                    |   ID:type ID:name                 {: RESULT = new AST_ID_LIST(type,name,null);:}
                    ;

expList 	::=	        exp:e COMMA expList:l   {: RESULT = new AST_EXP_LIST(e,l);    :}
					|   exp:e 				    {: RESULT = new AST_EXP_LIST(e,null); :}
					;

stmtList	::=		    stmt:s	stmtList:l	{: RESULT = new AST_STMT_LIST(s,l);    :}
					|   stmt:s				{: RESULT = new AST_STMT_LIST(s,null); :}
					;

cFieldList  ::=         cField:f cFieldList:l   {: RESULT = new AST_CFIELD_LIST(f,l);   :}
                    |   cField:f                {: RESULT = new AST_CFIELD_LIST(f,null);:}
                    ;
